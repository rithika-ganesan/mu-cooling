#########################################################################
#						   			#
#    Matching section isolated from Yuri Alexahin's HFOFO v7 channel    #
#  									#
#########################################################################

##### Global parameters ######

param channelname=mtchfullbls18p19
param histoFile=/mu-cooling/output/$channelname.root

### Constants, physics
param pi=3.1415926
physics QGSP_BIC

### Materials
material GH2 Z=1 A=1.01 density=0.014 #20% pf LH2
param worldMaterial=GH2

### Main
param zshift=60
param period=4200 #HFOFO period
trackcuts keep=mu+,mu-,pi+,pi- kineticEnergyCut=50 ###R: 50 MeV
param maxStep=5 steppingVerbose=0 

###### Declare elements ######

#================= Beam =================

param beamstart=-700.0 #From the rotator solenoid exit at z=0
param beamtime=-0.671 #average time of mu+ in initial.dat modulo RF period TRF

param toffs0=$beamtime-$beamstart/275.89-.07 # time needed to travel from beamZ to 0
param toffs1=$toffs0-.061-.0026+.0115+.006 # timing on the beam core done iteratively
param toffs2=$toffs1-.064+.149-.0052-.0029

#if there is no mu+ take mu- and add TRF/2

beam gaussian particle=mu+ \
	nEvents=100 \ 
	beamZ=$beamstart \
	sigmaX=0.0 \
	sigmaY=0.0 \
	sigmaXp=0.001 \
	sigmaYp=0.001 \
	meanMomentum=200.0 \
	sigmaP=0.0 \
	meanT=0.0 \
	sigmaT=0.0

reference particle=mu+ \
	beamX=0.0 beamY=0.0 \
	beamZ=$beamstart \ 
	beamXp=0 beamYp=0 \
	referenceMomentum=200.0 

#============== RF Cavity ===============

param Grad=25
param Grad0=20

pillbox RFC0 innerLength=249 frequency=0.325 maxGradient=$Grad0 \
win1Thick=0 win1OuterRadius=0 collarThick=0 collarRadialThick=0 \
winMat=Be irisRadius=300 win2Thick=0.12 wallThick=0.4 pipeThick=0.4 

pillbox RFC innerLength=249 frequency=0.325 maxGradient=$Grad \
win1Thick=0 win1OuterRadius=0 collarThick=0 collarRadialThick=0 \
winMat=Be irisRadius=300 win2Thick=0.12 wallThick=0.4 pipeThick=0.4 cavityMaterial=GH2 kill=1

#============= Collimators ==============

tubs abtube length=160 innerRadius=500 outerRadius=550 material=Vacuum kill=1 color=invisible

tubs terminus length=1 innerRadius=0 outerRadius=360 material=Vacuum kill=1 color=1,1,1

# LiH wedge absorber

trap wedge0 height=350 length=700 upperWidth=0.005 lowerWidth=59.5 material=LITHIUM_HYDRIDE color=0,0,1 

#============ Pressure wall =============

tubs presswall length=4 outerRadius=360 material=Stainless316 color=invisible

#========= Coils and solenoids ==========

coil kat0 innerRadius=360 outerRadius=500 length=6000 nSheets=10 maxR=360 maxZ=9000 dR=20 dZ=20 tolerance=0.003

solenoid RotSol coilName=kat0 current=11.37 kill=1

coil kat11 innerRadius=420 outerRadius=600 length=300 nSheets=10 maxR=360 maxZ=6000 dR=15 dZ=15 tolerance=0.003

param pitch=-0.0025*180/$pi
param BLS=18.19 delf=0
param cur0=4.421*$BLS
param curpl=$cur0*(1+$delf/2) curmn=-$cur0*(1-$delf/2)
param cur1=4.421*10.98 cur2=-4.421*17.89 cur3=4.421*19.64 cur4=-4.421*21.12 
param pitch3=0.08864577034195348 roll3=-122.41026081625786
param pitch4=0.12464581967694571 roll4=-23.560570626769156
param pitch5=0.08629462092385154 roll5=122.33834527427146
param pitch6=0.08165184417494886 roll6=-102.00442842295992
param pitch7=0.09691027250132747 roll7=25.337767430563712
param pitch8=0.16724456260285453 roll8=137.78049184460176
param pitch9=0.12260330888794266 roll9=-96.95844485200807

solenoid SolPos coilName=kat11 current=$curpl kill=1 color=1,0,1
solenoid SolNeg coilName=kat11 current=$curmn kill=1 color=0,0,1


####### Place elements #######

place RotSol z=-3000

place terminus z=-1000 #to stop reflected particles 

place SolPos z=500 current=$cur1
place SolNeg z=1200 current=$cur2
place SolPos z=1900 current=$cur3 rotation=X$pitch3,Z$roll3
place SolNeg z=2600 current=$cur4 rotation=X$pitch4,Z$roll4
place SolPos z=3300 rotation=X$pitch5,Z$roll5
place SolNeg z=4000 rotation=X$pitch6,Z$roll6 

# following section
  
place SolPos z=500+$period*1 rotation=X$pitch7,Z$roll7
place SolNeg z=1200+$period*1 rotation=X$pitch8,Z$roll8
place SolPos z=1900+$period*1 rotation=X$pitch9,Z$roll9
place SolNeg z=2600+$period*1 current=-94.482 rotation=X$pitch,Z240
place SolPos z=3300+$period*1 current=94.307 rotation=X$pitch
place SolNeg z=4000+$period*1 current=-94.136 rotation=X$pitch,Z120

place RFC0 z=-425. timeOffset=$toffs0-0.77123842
place RFC0 z=-175. timeOffset=$toffs0+0.13491993
place RFC0 z=75. timeOffset=$toffs0+1.0410783
place RFC0 z=325. timeOffset=$toffs0+1.9472366
place RFC0 z=575. timeOffset=$toffs0+2.853395
place RFC z=1075 timeOffset=$toffs1+4.6712809
place RFC z=1325 timeOffset=$toffs1+5.5805068
place RFC z=1775 timeOffset=$toffs1+7.2180941
place RFC z=2025 timeOffset=$toffs1+8.1284101
place RFC z=2475 timeOffset=$toffs1+9.7679614
place RFC z=2725 timeOffset=$toffs1+10.679369
place RFC z=3175 timeOffset=$toffs1+12.320888
place RFC z=3425 timeOffset=$toffs1+13.23339
place RFC z=3875 timeOffset=$toffs1+14.876879
place RFC z=4125 timeOffset=$toffs1+15.790477
place RFC z=4575 timeOffset=$toffs1+17.43594
place RFC z=4825 timeOffset=$toffs1+18.350636
place RFC z=5275 timeOffset=$toffs1+19.998077
place RFC z=5525 timeOffset=$toffs1+20.913873
place RFC z=5975 timeOffset=$toffs1+22.563296
place RFC z=6225 timeOffset=$toffs1+23.480193
place RFC z=6675 timeOffset=$toffs2+25.132058
place RFC z=6925 timeOffset=$toffs2+26.049908
place RFC z=7375 timeOffset=$toffs2+27.702303
place RFC z=7625 timeOffset=$toffs2+28.620446
place RFC z=8075 timeOffset=$toffs2+30.273366
place RFC z=8325 timeOffset=$toffs2+31.191799

place presswall z=850
place abtube z=850
place abtube z=1550
place abtube z=2250
place abtube z=2950
place abtube z=3650

place abtube z=150+$period*1
place abtube z=850+$period*1
place abtube z=1550+$period*1
place wedge0 x=-79.11 y=-155.4 rotation=Y90,Z333.0 lowerWidth=59.326 z=2250+$period*1
place abtube z=2250+$period*1
place wedge0 x=174.2 y=9.204 rotation=Y90,Z453.0 lowerWidth=58.783 z=2950+$period*1
place abtube z=2950+$period*1
place wedge0 x=-95.05 y=146.2 rotation=Y90,Z573.0 lowerWidth=58.239 z=3650+$period*1
place abtube z=3650+$period*1

trace nTrace=10 oneNTuple=1
